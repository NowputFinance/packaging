# Maintainer: Claudio Kozick√Ω <claudiokozicky@gmail.com>
# Contributor: peerchemist <peerchemist@protonmail.ch>

pkgname=('nowp-cli' 'nowp-daemon' 'nowp-qt' 'nowp-tx')
pkgbase='nowp'
pkgver=0.12.3.2
pkgrel=2
pkgdesc="A peer-to-peer network-based digital currency"
arch=('x86_64')
url="https://www.nowput.org/"
license=('MIT')
depends=('boost-libs' 'db' 'libevent' 'miniupnpc' 'openssl' 'qrencode' 'qt5-base' 'zeromq')
makedepends=('boost' 'qt5-tools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/0xcircuitbreaker/nowp/archive/refs/tags/v${pkgver}nowp.tar.gz")
b2sums=('208c6357766afb9991cfc09d43076b3c475dce4d3a62355e7321e693b9a585755d730b535f0c4ef049422c868fe4302372f719bd54e7e5b772d8645a5d2be81a')

build() {
    cd "$pkgbase-${pkgver}nowp"
    ./autogen.sh
    ./configure --prefix /usr --with-incompatible-bdb
    make
}

check() {
    cd "$pkgbase-${pkgver}nowp"
    make --keep-going check
}

package_nowp-cli() {
    pkgdesc="A peer-to-peer network-based digital currency - RPC client"
    depends=('boost-libs' 'libevent')

    cd "$pkgbase-${pkgver}nowp"
    install -D --mode 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    install -D --mode 644 \
            contrib/nowp-cli.bash-completion \
            "$pkgdir/usr/share/bash-completion/completions/nowp-cli"
    install -D --mode 644 \
            doc/man/nowp-cli.1 \
            "$pkgdir/usr/share/man/man1/nowp-cli.1"
    install -D --mode 755 \
            src/nowp-cli \
            "$pkgdir/usr/bin/nowp-cli"
}

package_nowp-daemon() {
    pkgdesc="A peer-to-peer network-based digital currency - daemon"
    depends=('boost-libs' 'db' 'libevent' 'miniupnpc' 'zeromq')
    optdepeneds=('tor')
    backup=('etc/nowp/nowp.conf')

    cd "$pkgbase-${pkgver}nowp"
    install -D --mode 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    install -D --mode 644 \
            share/examples/nowp.conf \
            "$pkgdir/etc/nowp/nowp.conf"
    install -D --mode 644 \
            contrib/nowpd.bash-completion \
            "$pkgdir/usr/share/bash-completion/completions/nowpd"
    install -D --mode 644 \
            contrib/systemd/nowpd@.service \
            "$pkgdir/usr/lib/systemd/system/nowpd@.service"
    install -D --mode 644 \
            contrib/systemd/nowpd-tor@.service \
            "$pkgdir/usr/lib/systemd/system/nowpd-tor@.service"
    install -D --mode 644 \
            contrib/debian/manpages/nowp.conf.5 \
            "$pkgdir/usr/share/man/man5/nowp.conf.5"
    install -D --mode 644 \
            doc/man/nowpd.1 \
            "$pkgdir/usr/share/man/man1/nowpd.1"
    install -D --mode 755 \
            src/nowpd \
            "$pkgdir/usr/bin/nowpd"
}

package_nowp-qt() {
    pkgdesc="A peer-to-peer network-based digital currency - Qt"
    depends=('boost-libs' 'db' 'libevent' 'miniupnpc' 'openssl' 'qrencode' 'qt5-base' 'zeromq')

    cd "$pkgbase-${pkgver}nowp"
    install -D --mode 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    install -D --mode 644 \
            contrib/debian/nowp-qt.desktop \
            "$pkgdir/usr/share/applications/nowp-qt.desktop"
    install -D --mode 644 \
            doc/man/nowp-qt.1 \
            "$pkgdir/usr/share/man/man1/nowp-qt.1"
    install -D --mode 644 \
            share/pixmaps/nowp128.png \
            "$pkgdir/usr/share/pixmaps/nowp128.png"
    install -D --mode 755 \
            src/qt/nowp-qt \
            "$pkgdir/usr/bin/nowp-qt"
}

package_nowp-tx() {
    pkgdesc="A peer-to-peer network-based digital currency - transaction tool"
    depends=('boost-libs')

    cd "$pkgbase-${pkgver}nowp"
    install -D --mode 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    install -D --mode 644 \
            contrib/nowp-tx.bash-completion \
            "$pkgdir/usr/share/bash-completion/completions/nowp-tx"
    install -D --mode 644 \
            doc/man/nowp-tx.1 \
            "$pkgdir/usr/share/man/man1/nowp-tx.1"
    install -D --mode 755 \
            src/nowp-tx \
            "$pkgdir/usr/bin/nowp-tx"
}
